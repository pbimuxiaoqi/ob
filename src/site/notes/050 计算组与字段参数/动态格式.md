---
{"dg-publish":true,"permalink":"/050 计算组与字段参数/动态格式/","tags":["计算组","动态格式"]}
---

计算组中格式表达式中可以设置度量的显示格式，这里要注意，**动态格式表达式目前只适用于值类型的度量，文本类型是不支持的**，比如最近很多人在表格中应用了svg，在图表中是正常显示成图表的，但在工具提示中就会显示长长的svg代码，可读性极差，目前只能通过添加图层或者指定工具提示页来解决，是无法修改svg度量的动态格式的，因为它是文本类型。

继续回到动态格式，在以前的文章中曾分享过怎么实现动态格式，比如当值大于亿时显示单位为亿，大于万时显示为万，就像下面这样

```JavaScript
VAR Cur =
    SELECTEDMEASURE ()
RETURN
    SWITCH (
        True(),
        Cur >= 1E8, """" & FORMAT ( Cur / 1E8, "#,0.00 亿" ),
        Cur >= 1E4, """" & FORMAT ( Cur / 1E4, "#,0.00 万" ),
        Cur >= 1E0, "#,0",
        "#,0.00%"
    )
```

![](https://secure2.wostatic.cn/static/4dPsKhybq3G9aYNuB8pYzk/image.png?auth_key=1705644876-uZYzfKVxAaX24rJGi8tTa3-0-c3b49bcd49c04cf9fd6eea69960ca662)

这段代码在值大于0的时候表现良好，但是当值存在负值时呢？首先想到的是用绝对值，会如我所愿吗？

```JavaScript
VAR Cur =
    SELECTEDMEASURE ()
var AbsCur = abs( Cur )
RETURN
    SWITCH (
        True(),
        AbsCur >= 1E8, """" & FORMAT ( Cur / 1E8, "#,0.00 亿" ),
        AbsCur >= 1E4, """" & FORMAT ( Cur / 1E4, "#,0.00 万" ),
        AbsCur >= 1E0, "#,0",
        "#,0.00%"
    )
```

![](https://secure2.wostatic.cn/static/58xRvQgW9eqSaw7NKcBycp/image.png?auth_key=1705644876-7CLZdSqcm7YpM5bKNDDk6K-0-deb15820648eeeb768cd2d38aba028df)

负值的部分出现了两个负号，那就问下gpt4，因为当值是负值时，本身会带一个负号，然后format函数又加了一个负号，就变成了两个负号。

![](https://secure2.wostatic.cn/static/hxuFf6PkQUA6XsQ9mugpmo/image.png?auth_key=1705644876-61HV75mws2sHP6it38TFNg-0-0c51dbbc2b6af0c9fee908c4ab2c447f)

明白了原因，处理起来就简单了，只需要保留自带的负号，Format格式化的时候全用绝对值就可以了。

```DAX
VAR Cur =
    SELECTEDMEASURE ()
var AbsCur = abs( Cur )
var result = 
    SWITCH (
        True(),
        AbsCur >= 1E8, """" & FORMAT ( AbsCur / 1E8, "#,0.00 亿" ),
        AbsCur >= 1E4, """" & FORMAT ( AbsCur / 1E4, "#,0.00 万" ),
        AbsCur >= 1E0, "#,0",
        "#,0"
    )
 return result
```

![](https://secure2.wostatic.cn/static/h4NxoDy4ssFAtzNJn2ZpDJ/image.png?auth_key=1705644876-cktf6KH3MUp7YVsbPq8W57-0-ff4291be915c52ccd6246de65abc28ad)

如果想单独设置负值时的格式呢？回到微软官方文档，发现[FORMAT](https://learn.microsoft.com/zh-cn/dax/format-function-dax)的自定义格式部分其实是有三部分呢，只是通常正值和负值的格式一样，所以就没用过后面的部分。。。

![](https://secure2.wostatic.cn/static/iUspVvngFZv34KPs4xu6dr/image.png?auth_key=1705644876-qm4VV4mEdZr3KFkJLcDNRA-0-993bc617f0c1a92bbc071fe4a4552deb)

所以，也可以改为下面这样

```js
VAR Cur =
    SELECTEDMEASURE ()
var AbsCur = ABS( Cur )
RETURN
    SWITCH (
        True(),
        AbsCur >= 1E8, """" & FORMAT ( Cur / 1E8, "#,0.00 亿;#,0.00 亿 " ),
        AbsCur >= 1E4, """" & FORMAT ( Cur / 1E4, "#,0.00 万;#,0.00 万" ),
        AbsCur >= 1E0, "#,0",
        "#,0.00%"
    )
```

